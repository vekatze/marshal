import {
  binary-builder.entity {builder, create, get},
  binary-builder.scene {append-int8},
  core.binary {binary, binary-access, binary-length},
  core.cell {new-cell},
  core.either {Left, Right, either},
  core.int.show {show-int},
  core.magic,
  this.class {Marshal, _proceed, marshal},
  this.class.bool,
  this.class.int64,
}

inline encode-either<a, b>(m1: marshal(a), m2: marshal(b)): (buf: &builder, x-or-y: &either(a, b)) -> unit {
  function (buf: &builder, x-or-y: &either(a, b)): unit {
    case x-or-y {
    | Left(x) =>
      let Marshal of {encode} = m1 in
      append-int8(buf, 0);
      encode(buf, x)
    | Right(y) =>
      let Marshal of {encode} = m2 in
      append-int8(buf, 1);
      encode(buf, y)
    }
  }
}

inline decode-either<a, b>(m1: marshal(a), m2: marshal(b)): (buf: &binary, cursor-ref: &cell(int)) -> ?either(a, b) {
  function (buf: &binary, cursor-ref: &cell(int)): ?either(a, b) {
    let cursor = core.cell.clone(cursor-ref) in
    if lt-int(binary-length(buf), add-int(cursor, 1)) {
      none
    } else {
      let v = binary-access(buf, add-int(cursor, 0)) in
      if eq-int8(v, 0) {
        _proceed(cursor-ref, 1);
        let Marshal of {decode} = m1 in
        try l = decode(buf, cursor-ref) in
        OK(Left(l))
      } else-if eq-int8(v, 1) {
        _proceed(cursor-ref, 1);
        let Marshal of {decode} = m2 in
        try r = decode(buf, cursor-ref) in
        OK(Right(r))
      } else {
        none
      }
    }
  }
}

inline necess-either<a, b>(_: marshal(a), _: marshal(b)): (either(a, b)) -> meta either(a, b) {
  function (x) {
    core.magic.cast(either(a, b), meta either(a, b), x)
  }
}

inline as-marshal<a, b>(!m1: marshal(a), !m2: marshal(b)): marshal(either(a, b)) {
  Marshal of {
    encode = encode-either(!m1, !m2),
    decode = decode-either(!m1, !m2),
    necess = necess-either(!m1, !m2),
  }
}

inline either-int-bool: marshal(either(int, bool)) {
  as-marshal(this.class.int64.as-marshal, this.class.bool.as-marshal)
}

define zen(): unit {
  let Marshal of {encode, decode} = either-int-bool in
  let buf = create(8) in
  pin val = Left(-1234) in
  let _ on buf = encode(buf, val) in
  pin b = get(buf) in
  pin cursor-ref = new-cell(0) in
  let mval = decode(b, cursor-ref) in
  match mval {
  | Error(_) =>
    print("error\n")
  | OK(x-or-y) =>
    match x-or-y {
    | Left(x) =>
      printf("int: {}\n", [show-int(x)])
    | Right(_) =>
      print("bool\n")
    }
  }
}
