import {
  binary-builder.entity {builder, create, get},
  core.binary {binary},
  core.bool.show,
  core.cell {cell},
  core.int.show {show-int},
  core.magic,
  this.class {Marshal, marshal},
  this.class.bool,
  this.class.int64,
}

inline encode-pair<a, b>(m1: marshal(a), m2: marshal(b)): (buf: &builder, xy: &pair(a, b)) -> unit {
  function (buf: &builder, xy: &pair(a, b)): unit {
    tie Pair(x, y) = xy in
    let Marshal of {encode = encode1} = m1 in
    let Marshal of {encode = encode2} = m2 in
    encode1(buf, x);
    encode2(buf, y)
  }
}

inline decode-pair<a, b>(m1: marshal(a), m2: marshal(b)): (buf: &binary, cursor-ref: &cell(int)) -> ?pair(a, b) {
  function (buf: &binary, cursor-ref: &cell(int)): ?pair(a, b) {
    let Marshal of {decode = decode1} = m1 in
    let Marshal of {decode = decode2} = m2 in
    try l = decode1(buf, cursor-ref) in
    try r = decode2(buf, cursor-ref) in
    Right(Pair(l, r))
  }
}

inline necess-pair<a, b>(_: marshal(a), _: marshal(b)): (pair(a, b)) -> meta pair(a, b) {
  function (x) {
    core.magic.cast(pair(a, b), meta pair(a, b), x)
  }
}

inline as-marshal<a, b>(!m1: marshal(a), !m2: marshal(b)): marshal(pair(a, b)) {
  Marshal of {
    encode = encode-pair(!m1, !m2),
    decode = decode-pair(!m1, !m2),
    necess = necess-pair(!m1, !m2),
  }
}

constant pair-int-bool: marshal(pair(int, bool)) {
  as-marshal(this.class.int64.as-marshal, this.class.bool.as-marshal)
}

define zen(): unit {
  let buf = create(8) in
  pin val = Pair(-1234, True) in
  let Marshal of {encode, decode} = pair-int-bool in
  let _ on buf = encode(buf, val) in
  pin b = get(buf) in
  pin cursor-ref = new-cell(0) in
  let mval = decode(b, cursor-ref) in
  match mval {
  | Left(_) =>
    print("left\n")
  | Right(xy) =>
    let Pair(x, y) = xy in
    printf("int: {}\n", [show-int(x)]);
    printf("bool: {}\n", [core.bool.show.show(y)])
  }
}
