import {
  core.box {axiom-T},
  core.int.show {show-int},
  this.move.class.bool,
  this.move.class.int64,
  this.move.common.get {get-int8},
  this.move.common.put {get, save-int8},
  this.move.create-decode-handle {from-bytes},
  this.move.create-encode-handle {create-encode-handle},
  this.rule.decode-handle {decode-handle},
  this.rule.encode-handle {encode-handle},
  this.rule.marshal {Marshal, marshal},
}

inline encode-either<i1, o1, i2, o2>(
  m1: marshal(i1, o1),
  m2: marshal(i2, o2),
): (h: &encode-handle, x-or-y: &either(i1, i2)) -> unit {
  function (h, x-or-y) {
    case x-or-y {
    | Left(x) =>
      let Marshal of {encode} = m1 in
      save-int8(h, 0);
      encode(h, x)
    | Right(y) =>
      let Marshal of {encode} = m2 in
      save-int8(h, 1);
      encode(h, y)
    }
  }
}

inline decode-either<i1, o1, i2, o2>(
  m1: marshal(i1, o1),
  m2: marshal(i2, o2),
): (h: &decode-handle) -> ?meta either(o1, o2) {
  function (h) {
    try tag = get-int8(h) in
    if eq-int8(tag, 0) {
      let Marshal of {decode} = m1 in
      try l = decode(h) in
      Right(box {
        letbox l' = l in
        Left(l')
      })
    } else-if eq-int8(tag, 1) {
      let Marshal of {decode} = m2 in
      try r = decode(h) in
      Right(box {
        letbox r' = r in
        Right(r')
      })
    } else {
      none
    }
  }
}

inline as-marshal<i1, o1, i2, o2>(
  !m1: marshal(i1, o1),
  !m2: marshal(i2, o2),
): marshal(either(i1, i2), either(o1, o2)) {
  Marshal of {
    encode = encode-either(!m1, !m2),
    decode = decode-either(!m1, !m2),
  }
}

inline either-int-bool: marshal(either(int, bool), either(int, bool)) {
  as-marshal(this.move.class.int64.as-marshal, this.move.class.bool.as-marshal)
}

define zen(): unit {
  let Marshal of {encode, decode} = either-int-bool in
  let h-save = create-encode-handle(8) in
  pin val = Left(-1234) in
  let _ on h-save = encode(h-save, val) in
  let b = get(h-save) in
  pin h = from-bytes(b) in
  let mval = decode(h) in
  match mval {
  | Left(_) =>
    print("error\n")
  | Right(x-or-y) =>
    match axiom-T(x-or-y) {
    | Left(x) =>
      printf("int: {}\n", [show-int(x)])
    | Right(_) =>
      print("bool\n")
    }
  }
}
