import {
  binary-builder.entity {builder, create, get},
  binary-builder.scene {append-float64-big-endian},
  core.box {axiom-T},
  core.float.show {show-float},
  this.move.common.cursor {proceed-int64},
  this.move.create-decode-handle {from-bytes},
  this.rule.decode-handle {decode-handle},
  this.rule.marshal {Marshal, marshal},
}

define encode-float64(b: &builder, x: &float64): unit {
  append-float64-big-endian(b, *x)
}

define decode-float64(h: &decode-handle): ?meta float64 {
  try v = proceed-int64(h) in
  Right(quote {magic cast(int64, float64, v)})
}

inline as-marshal: marshal(float64, float64) {
  Marshal of {
    encode = encode-float64,
    decode = decode-float64,
  }
}

define zen(): unit {
  let buf = create(8) in
  pin val = -3.8 in
  let _ on buf = encode-float64(buf, val) in
  let b = get(buf) in
  pin h = from-bytes(b) in
  let mval = decode-float64(h) in
  match mval {
  | Left(_) =>
    print("left")
  | Right(v) =>
    printf("value: {}\n", [show-float(axiom-T(v))])
  }
}
