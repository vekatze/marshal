import {
  binary-builder.entity {builder, create, get},
  binary-builder.scene {append-float64-big-endian},
  core.binary {binary},
  core.box {axiom-T},
  core.control {discard},
  core.float.show {show-float},
  this.move.common.cursor {proceed-int64},
  this.rule.marshal {Marshal, marshal},
}

define encode-float64(b: &builder, x: &float64): unit {
  append-float64-big-endian(b, *x)
}

define decode-float64(b: &binary, cursor-ref: &cell(int)): ?meta float64 {
  try v = proceed-int64(b, cursor-ref) in
  Right(quote {magic cast(int64, float64, v)})
}

inline as-marshal: marshal(float64, float64) {
  Marshal of {
    encode = encode-float64,
    decode = decode-float64,
  }
}

define zen(): unit {
  let buf = create(8) in
  pin val = -3.8 in
  let _ on buf = encode-float64(buf, val) in
  let b = get(buf) in
  pin cursor-ref = new-cell(0) in
  let mval on b = decode-float64(b, cursor-ref) in
  discard(b);
  match mval {
  | Left(_) =>
    print("left")
  | Right(v) =>
    printf("value: {}\n", [show-float(axiom-T(v))])
  }
}
