import {
  core.box {axiom-T},
  core.int.show {show-int},
  core.list {for},
  this.move.class.int64,
  this.move.common.get {get-int8},
  this.move.common.put {extract-binary, put-int8},
  this.move.create-get-handle {from-bytes},
  this.move.create-put-handle {create-put-handle},
  this.rule.get-handle {get-handle},
  this.rule.marshal {Marshal, marshal},
  this.rule.put-handle {put-handle},
}

inline _put<i, o>(!m: marshal(i, o)): (h: &put-handle, xs: &list(i)) -> unit {
  define self(h, xs): unit {
    case xs {
    | Nil =>
      put-int8(h, 0)
    | Cons(y, ys) =>
      let Marshal of {put} = !m in
      put-int8(h, 1);
      put(h, y);
      self(h, ys)
    }
  }
}

inline _get<i, o>(!m: marshal(i, o)): (h: &get-handle) -> ?meta list(o) {
  define self(h) {
    try tag = get-int8(h) in
    if eq-int8(tag, 0) {
      Right(box {Nil})
    } else-if eq-int8(tag, 1) {
      let Marshal of {get} = !m in
      try v = get(h) in
      try vs = self(h) in
      Right(box {
        letbox v' = v in
        letbox vs' = vs in
        Cons(v', vs')
      })
    } else {
      none
    }
  }
}

inline as-marshal<i, o>(!m: marshal(i, o)): marshal(list(i), list(o)) {
  Marshal of {
    put = _put(!m),
    get = _get(!m),
  }
}

inline _int-list: marshal(list(int), list(int)) {
  as-marshal(this.move.class.int64.as-marshal)
}

define zen(): unit {
  let Marshal of {put, get} = _int-list in
  let h-save = create-put-handle(8) in
  pin val = [1, 2, -123, 1384910234] in
  let _ on h-save = put(h-save, val) in
  let b = extract-binary(h-save) in
  pin h = from-bytes(b) in
  let mval = get(h) in
  match mval {
  | Left(_) =>
    print("left\n")
  | Right(vs) =>
    print("int-list\n");
    for(axiom-T(vs), function (v) {
      printf("val: {}\n", [show-int(v)])
    })
  }
}
