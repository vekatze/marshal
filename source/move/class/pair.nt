import {
  core.bool.show,
  core.int.show {show-int},
  this.move.class.bool {bool-marshal},
  this.move.class.int64 {int64-marshal},
  this.move.decode {decode},
  this.move.encode {encode},
  this.rule.get-handle {get-handle},
  this.rule.marshal {Marshal, marshal},
  this.rule.put-handle {put-handle},
}

inline _put<i1, o1, i2, o2>(
  m1: marshal(i1, o1),
  m2: marshal(i2, o2),
): (h: &put-handle, xy: &pair(i1, i2)) -> unit {
  function (h, xy) {
    tie Pair(x, y) = xy in
    let Marshal of {put = put1} = m1 in
    let Marshal of {put = put2} = m2 in
    put1(h, x);
    put2(h, y)
  }
}

inline _get<i1, o1, i2, o2>(
  m1: marshal(i1, o1),
  m2: marshal(i2, o2),
): (h: &get-handle) -> ?meta pair(o1, o2) {
  function (h) {
    let Marshal of {get = get1} = m1 in
    let Marshal of {get = get2} = m2 in
    try l = get1(h) in
    try r = get2(h) in
    Right(box {
      letbox l' = l in
      letbox r' = r in
      Pair(l', r')
    })
  }
}

inline pair-marshal<i1, o1, i2, o2>(
  !m1: marshal(i1, o1),
  !m2: marshal(i2, o2),
): marshal(pair(i1, i2), pair(o1, o2)) {
  Marshal of {
    put = _put(!m1, !m2),
    get = _get(!m1, !m2),
  }
}

inline _pair-int-bool: marshal(pair(int, bool), pair(int, bool)) {
  pair-marshal(int64-marshal, bool-marshal)
}

define zen(): unit {
  pin val = Pair(-1234, True) in
  match decode(_pair-int-bool, encode(_pair-int-bool, val, 8)) {
  | Left(_) =>
    print("left\n")
  | Right(xy) =>
    let Pair(x, y) = xy in
    printf("int: {}\n", [show-int(x)]);
    printf("bool: {}\n", [core.bool.show.show(y)])
  }
}
