import {
  core.bool.show,
  core.box {axiom-T},
  core.int.show {show-int},
  this.move.class.bool,
  this.move.class.int64,
  this.move.common.put {get},
  this.move.create-decode-handle {from-bytes},
  this.move.create-encode-handle {create-encode-handle},
  this.rule.get-handle {decode-handle},
  this.rule.put-handle {encode-handle},
  this.rule.marshal {Marshal, marshal},
}

inline encode-pair<i1, o1, i2, o2>(
  m1: marshal(i1, o1),
  m2: marshal(i2, o2),
): (h: &encode-handle, xy: &pair(i1, i2)) -> unit {
  function (h, xy) {
    tie Pair(x, y) = xy in
    let Marshal of {encode = encode1} = m1 in
    let Marshal of {encode = encode2} = m2 in
    encode1(h, x);
    encode2(h, y)
  }
}

inline decode-pair<i1, o1, i2, o2>(
  m1: marshal(i1, o1),
  m2: marshal(i2, o2),
): (h: &decode-handle) -> ?meta pair(o1, o2) {
  function (h) {
    let Marshal of {decode = decode1} = m1 in
    let Marshal of {decode = decode2} = m2 in
    try l = decode1(h) in
    try r = decode2(h) in
    Right(box {
      letbox l' = l in
      letbox r' = r in
      Pair(l', r')
    })
  }
}

inline as-marshal<i1, o1, i2, o2>(
  !m1: marshal(i1, o1),
  !m2: marshal(i2, o2),
): marshal(pair(i1, i2), pair(o1, o2)) {
  Marshal of {
    encode = encode-pair(!m1, !m2),
    decode = decode-pair(!m1, !m2),
  }
}

inline pair-int-bool: marshal(pair(int, bool), pair(int, bool)) {
  as-marshal(this.move.class.int64.as-marshal, this.move.class.bool.as-marshal)
}

define zen(): unit {
  let h-save = create-encode-handle(8) in
  pin val = Pair(-1234, True) in
  let Marshal of {encode, decode} = pair-int-bool in
  let _ on h-save = encode(h-save, val) in
  let b = get(h-save) in
  pin h = from-bytes(b) in
  let mval = decode(h) in
  match mval {
  | Left(_) =>
    print("left\n")
  | Right(xy) =>
    let Pair(x, y) = axiom-T(xy) in
    printf("int: {}\n", [show-int(x)]);
    printf("bool: {}\n", [core.bool.show.show(y)])
  }
}
