import {
  binary-builder.entity {builder, create, get},
  core.box {axiom-T},
  core.int.show {show-int},
  core.vector {vector},
  this.move.class.int64 {decode-int64, encode-int64},
  this.move.create-decode-handle {from-bytes},
  this.rule.decode-handle {decode-handle},
  this.rule.marshal {Marshal, marshal},
}

inline encode-vector<a1, a2>(m: marshal(a1, a2)): (b: &builder, xs: &vector(a1)) -> unit {
  function (b: &builder, xs: &vector(a1)): unit {
    pin len = core.vector.length(xs) in
    encode-int64(b, len);
    core.vector.for-N(xs, function (x, _) {
      let Marshal of {encode} = m in
      encode(b, x)
    })
  }
}

inline decode-vector<a1, a2>(m: marshal(a1, a2)): (h: &decode-handle) -> ?meta vector(a2) {
  function (h: &decode-handle): ?meta vector(a2) {
    try len = decode-int64(h) in
    try vs =
      core.vector.create-E(axiom-T(len), function (_) {
        let Marshal of {decode} = m in
        try value = decode(h) in
        Right(axiom-T(value))
      })
    in
    Right(magic cast(vector(a2), meta vector(a2), vs))
  }
}

inline as-marshal<a1, a2>(!m: marshal(a1, a2)): marshal(vector(a1), vector(a2)) {
  Marshal of {
    encode = encode-vector(!m),
    decode = decode-vector(!m),
  }
}

inline int-vector: marshal(vector(int), vector(int)) {
  as-marshal(this.move.class.int64.as-marshal)
}

define zen(): unit {
  let Marshal of {encode, decode} = int-vector in
  let buf = create(8) in
  pin val = core.vector.from-list([1, 2, -123, 1384910234]) in
  let _ on buf = encode(buf, val) in
  let b = get(buf) in
  pin h = from-bytes(b) in
  let mval = decode(h) in
  match mval {
  | Left(_) =>
    print("left\n")
  | Right(vs) =>
    print("int-vector\n");
    pin vs = axiom-T(vs) in
    core.vector.for-N(vs, function (v, _) {
      printf("val: {}\n", [show-int(*v)])
    })
  }
}
