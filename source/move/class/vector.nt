import {
  core.box {axiom-T},
  core.int.show {show-int},
  core.vector {vector},
  this.move.class.int64 {int64-sero},
  this.move.common.get {get-int64},
  this.move.common.put {put-int64},
  this.move.decode {decode},
  this.move.encode {encode},
  this.rule.get-kit {get-kit},
  this.rule.put-kit {put-kit},
  this.rule.sero {Sero, sero},
}

inline _put<i, o>(m: sero(i, o)): (h: &put-kit, xs: &vector(i)) -> unit {
  function (h, xs) {
    let len = core.vector.length(xs) in
    put-int64(h, len);
    core.vector.for-N(xs, function (x, _) {
      let Sero of {put} = m in
      put(h, x)
    })
  }
}

inline _get<i, o>(m: sero(i, o)): (h: &get-kit) -> ?meta vector(o) {
  function (h) {
    try len = get-int64(h) in
    try vs =
      core.vector.create-E(len, function (_) {
        let Sero of {get} = m in
        try value = get(h) in
        Right(axiom-T(value))
      })
    in
    Right(magic cast(vector(o), meta vector(o), vs))
  }
}

inline vector-sero<i, o>(!m: sero(i, o)): sero(vector(i), vector(o)) {
  Sero of {
    put = _put(!m),
    get = _get(!m),
  }
}

inline _int-vector: sero(vector(int), vector(int)) {
  vector-sero(int64-sero)
}

define zen(): unit {
  pin val = core.vector.from-list([1, 2, -123, 1384910234]) in
  match decode(_int-vector, encode(_int-vector, val, 8)) {
  | Left(_) =>
    print("left\n")
  | Right(vs) =>
    print("int-vector\n");
    pin vs = vs in
    core.vector.for-N(vs, function (v, _) {
      printf("val: {}\n", [show-int(*v)])
    })
  }
}
