import {
  core.box {axiom-T},
  core.int.show {show-int},
  core.vector {vector},
  this.move.class.int64 {decode-int64},
  this.move.common.put {get, put-int64},
  this.move.create-get-handle {from-bytes},
  this.move.create-put-handle {create-put-handle},
  this.rule.get-handle {get-handle},
  this.rule.marshal {Marshal, marshal},
  this.rule.put-handle {put-handle},
}

inline encode-vector<i, o>(m: marshal(i, o)): (h: &put-handle, xs: &vector(i)) -> unit {
  function (h, xs) {
    let len = core.vector.length(xs) in
    put-int64(h, len);
    core.vector.for-N(xs, function (x, _) {
      let Marshal of {encode} = m in
      encode(h, x)
    })
  }
}

inline decode-vector<i, o>(m: marshal(i, o)): (h: &get-handle) -> ?meta vector(o) {
  function (h) {
    try len = decode-int64(h) in
    try vs =
      core.vector.create-E(axiom-T(len), function (_) {
        let Marshal of {decode} = m in
        try value = decode(h) in
        Right(axiom-T(value))
      })
    in
    Right(magic cast(vector(o), meta vector(o), vs))
  }
}

inline as-marshal<i, o>(!m: marshal(i, o)): marshal(vector(i), vector(o)) {
  Marshal of {
    encode = encode-vector(!m),
    decode = decode-vector(!m),
  }
}

inline int-vector: marshal(vector(int), vector(int)) {
  as-marshal(this.move.class.int64.as-marshal)
}

define zen(): unit {
  let Marshal of {encode, decode} = int-vector in
  let h-save = create-put-handle(8) in
  pin val = core.vector.from-list([1, 2, -123, 1384910234]) in
  let _ on h-save = encode(h-save, val) in
  let b = get(h-save) in
  pin h = from-bytes(b) in
  let mval = decode(h) in
  match mval {
  | Left(_) =>
    print("left\n")
  | Right(vs) =>
    print("int-vector\n");
    pin vs = axiom-T(vs) in
    core.vector.for-N(vs, function (v, _) {
      printf("val: {}\n", [show-int(*v)])
    })
  }
}
