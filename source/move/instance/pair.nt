import {
  core.bool.show,
  core.int.show {show-int},
  this.move.decode {decode},
  this.move.encode {encode},
  this.move.instance.bool {bool-sero},
  this.move.instance.int64 {int64-sero},
  this.rule.get-kit {get-kit},
  this.rule.put-kit {put-kit},
  this.rule.sero {Sero, sero},
}

inline _put<a, b>(m1: sero(a), m2: sero(b)): (k: &put-kit, xy: &pair(a, b)) -> unit {
  function (k, xy) {
    tie Pair(x, y) = xy in
    let Sero of {put = put1} = m1 in
    let Sero of {put = put2} = m2 in
    put1(k, x);
    put2(k, y)
  }
}

inline _get<a, b>(m1: sero(a), m2: sero(b)): (k: &get-kit) -> ?meta pair(a, b) {
  function (k) {
    let Sero of {get = get1} = m1 in
    let Sero of {get = get2} = m2 in
    try l = get1(k) in
    try r = get2(k) in
    Right(box {
      letbox l' = l in
      letbox r' = r in
      Pair(l', r')
    })
  }
}

inline pair-sero<a, b>(!m1: sero(a), !m2: sero(b)): sero(pair(a, b)) {
  Sero of {
    put = _put(!m1, !m2),
    get = _get(!m1, !m2),
  }
}

inline _pair-int-bool: sero(pair(int, bool)) {
  pair-sero(int64-sero, bool-sero)
}

define zen(): unit {
  pin val = Pair(-1234, True) in
  match decode(_pair-int-bool, encode(_pair-int-bool, val, 8)) {
  | Left(_) =>
    print("left\n")
  | Right(xy) =>
    let Pair(x, y) = xy in
    printf("int: {}\n", [show-int(x)]);
    printf("bool: {}\n", [core.bool.show.show(y)])
  }
}
